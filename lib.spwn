extract import "colors.spwn"
extract obj_props

type @ptnc
impl @ptnc {
    new: (level: [[@string]], distance: @number = 30) {
        return @ptnc::{
            level,
            distance,
            start: { x: 0, y: 0 },
            player: {
                body: { group: ?g, block: ?b },
                hand: { group: ?g, block: ?b },
            },
            adjacent_blocks: { group: ?g, block: ?b },
        }
    },
    display_level: (self, x_offset: @number, y_offset: @number) -> @dictionary {
        for y in 0..self.level.length {
            for x in 0..self.level[y].length {
                current_color = get_color(self.level[y][x])
        
                object_x = x_offset + x * self.distance
                object_y = y_offset + y * self.distance
        
                if self.level[y][x] == "S" {
                    self.start.x = object_x
                    self.start.y = object_y 
                }
        
                $.add(obj {
                    OBJ_ID: 211,
                    X: object_x,
                    Y: object_y,
                    COLOR: current_color.color,
                    SCALING: self.distance / 30,
                })
                $.add(obj {
                    OBJ_ID: 1816,
                    X: object_x,
                    Y: object_y,
                    BLOCK_A: current_color.block,
                    SCALING: 0.01,
                })
            }
        }
    },
    display_player: (self) -> @dictionary {
        $.add(obj {
            OBJ_ID: 1816,
            X: self.start.x,
            Y: self.start.y,
            BLOCK_A: self.player.body.block,
            GROUPS: self.player.body.group,
            SCALING: 0.1,
            DYNAMIC_BLOCK: true,
        })
        $.add(obj {
            OBJ_ID: 1816,
            X: self.start.x - self.distance,
            Y: self.start.y,
            BLOCK_A: self.player.hand.block,
            GROUPS: self.player.hand.group,
            SCALING: 0.1,
            DYNAMIC_BLOCK: true,
        })

        distances = [
            [self.distance, 0],
            [0, self.distance],
            [-self.distance, 0],
            [0, -self.distance]
        ]

        // electricity-water checker
        for [ x_off, y_off ] in distances {
            $.add(obj {
                OBJ_ID: 1816,
                X: self.start.x + x_off,
                Y: self.start.y + y_off,
                BLOCK_A: self.adjacent_blocks.block,
                GROUPS: self.adjacent_blocks.group,
                SCALING: 0.1,
                DYNAMIC_BLOCK: true,
            })
        }

        -> self.player.hand.group.follow(self.player.body.group)
        -> self.adjacent_blocks.group.follow(self.player.body.group)
    },
    parse_level: (input: @string) -> [[@string]] {
        lines = input.replace(r"\r?\n","\n").split("\n").filter(v => v.trim().length > 0)
        return lines.map(l => @array(l))
    },
}
